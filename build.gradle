plugins {
    id "java"
    id "maven-publish"
    id "me.dreamhopping.pml.gradle" version "3.5.1+build.11"
    id "org.bookmc.buildtools" version "0.3.+"
}

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

ext {
    def env = System.getenv()

    devLocal = false // Change this to true if you want local
    local = env.GITHUB_ACTIONS ? false : devLocal
    buildNumber = env.BUILD_NUMBER
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

group "org.bookmc"
version "0.5.4${branch != "main" ? "-$branch" : ""}+${buildNumber ? "build.$buildNumber" : "local"}"

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmc.org/"
    }
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }

    if (local) {
        mavenLocal()
    }
}

minecraft {
    separateVersionJars()

    target "1.17.1", "1.16.5", "1.15.2", "1.14.4", "1.13.2", "1.12.2", "1.11.2", "1.10.2", "1.9.4"
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:2.14.1"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"

    implementation("org.bookmc:book-loader:0.9.9-rentrance-issues+${local ? "local" : "build.18"}") {
        exclude module: "sponge-mixin"
    }

    annotationProcessor("org.spongepowered:mixin:0.8.3-SNAPSHOT:processor") {
        exclude module: "commons-io"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
    }

    compileOnly "com.google.code.gson:gson:2.8.8"
}


jar {
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class"
}

sourcesJar.enabled = false


publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            name local || !isEnvVarsAvailable ? "RepositoryFolder" : "BookMC"


            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}