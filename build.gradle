allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    sourceCompatibility = targetCompatibility = 1.8
    compileJava.options.encoding = "UTF-8"

    ext {
        def env = System.getenv()
        devLocal = false // Change this to true if you want local
        local = env.GITHUB_ACTIONS ? false : devLocal
        jdk = JavaVersion.current().getMajorVersion()
    }

    group "org.bookmc"
    // We add the suffix for (our) non-lts support.
    // Java 8 support will be removed in the future however.
    version "0.2.3${local ? "-local" : ""}"

    repositories {
        maven {
            url "https://maven.bookmc.org"
        }
        mavenCentral()
        if (local) {
            mavenLocal()
        }
    }

    configurations {
        include
        implementation.extendsFrom(include)
    }

    dependencies {
        compileOnly "org.bookmc:book-loader:0.8.7${local ? "-local" : ""}"
        include "org.bookmc:srg-processor:1.1.2"
    }

    processResources {
        inputs.files "src/main/resources"
        outputs.dir "build/classes/main"
        copy {
            from("src/main/resources")
            into("build/classes/main")
        }
    }

    jar {
        from {
            configurations.include.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }

        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        exclude "module-info.class"
    }

    publishing {
        repositories {
            maven {
                def env = System.getenv()

                boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

                if (isEnvVarsAvailable) {
                    credentials {
                        username env.get("MAVEN_USER")
                        password env.get("MAVEN_PASSWORD")
                    }
                }

                url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
            }
        }

        publications {
            //noinspection GroovyAssignabilityCheck
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = "book-loader"
                    url = "https://github.com/BookMC/loader"

                    developers {
                        developer {
                            name = "ChachyDev"
                        }
                    }
                }
            }
        }
    }
}