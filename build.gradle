plugins {
    id "java"
    id "maven-publish"
    id "me.dreamhopping.pml.gradle" version "3.0.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

ext {
    def env = System.getenv()

    devLocal = false // Change this to true if you want local
    local = env.GITHUB_ACTIONS ? false : devLocal
    jdk = JavaVersion.current().getMajorVersion()
}

group "org.bookmc"
version "0.3.0${local ? "-local" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmc.org"
    }
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }

    if (local) {
        mavenLocal()
    }
}

minecraft {
    separateVersionJars()

    clientRunClass = "org.bookmc.loader.impl.launch.QuiltClient"
    serverRunClass = "org.bookmc.loader.impl.launch.QuiltServer"


    target('1.17.1') {
        accessTransformer './src/mc1.17.1/resources/access.json'
        yarn(null)
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:2.14.1"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"

    implementation "org.bookmc:book-loader:0.8.20${local ? "-local" : ""}"

    include("org.bookmc:srg-processor:2.0.1") {
        exclude module: "log4j-api"
        exclude module: "log4j-core"
    }
}


jar {
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class"
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "book-loader"
                url = "https://github.com/BookMC/minecraft"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}