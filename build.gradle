plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "org.bookmc.buildtools" version "0.3.8+build.29"
    id "me.dreamhopping.pml.gradle" version "3.13.0+build.27"
    id "org.bookmc.annotation-processor-configuration" version "0.2.1+build.6"
}

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

ext {
    branch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

group "org.bookmc"
version "0.22.0${branch != "main" && !branch.isEmpty() ? "-$branch" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmc.org/releases/"
    }
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
}

minecraft {
    separateVersionJars()

    target "1.18-pre4",
        "1.18-pre3",
        "1.18-pre2",
        "1.18-pre1"
        "1.18_experimental-snapshot-5",
        "1.18_experimental-snapshot-4",
        "1.18_experimental-snapshot-3",
        "1.18_experimental-snapshot-2",
        "1.18_experimental-snapshot-1",
        "1.17.1",
        "1.16.5",
        "1.15.2",
        "1.14.4",
        "1.13.2",
        "1.12.2",
        "1.11.2",
        "1.10.2",
        "1.9.4",
        "1.8.9",
        "b1.7.3"
}

configurations {
    include
    implementation.extendsFrom(include)

    mcb173Include
    mcb173Implementation.extendsFrom(mcb173Include)
}

dependencies {
    // To be downloaded via the launcher itself
    implementation("org.bookmc:book-loader:1.0.0") {
        exclude module: "sponge-mixin"
    }

    // Minecraft already has these libraries we just need them for the common module
    compileOnly "com.google.code.gson:gson:2.8.8"

    // In all attempts to avoid log4j issues we must compile against
    // an older version of log4j due to receiving a NoSuchMethodError
    // TBE: java.lang.NoSuchMethodError: 'void org.apache.logging.log4j.Logger.info(java.lang.String, java.lang.Object, java.lang.Object)'
    compileOnly "org.apache.logging.log4j:log4j-api:2.0-beta9"
    compileOnly "org.apache.logging.log4j:log4j-core:2.0-beta9"

    // Minecraft B1.7.3 doesn't include GSON or Log4J and we use them
    // in hopes the game already has it available so we must embed it
    // ourselves
    mcb173Include "com.google.code.gson:gson:2.8.8"
    mcb173Include "org.apache.logging.log4j:log4j-api:2.0-beta9"
    mcb173Include "org.apache.logging.log4j:log4j-core:2.0-beta9"
}

sourcesJar.enabled = false

mcb173Jar {
    from {
        configurations.mcb173Include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jar {
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean login = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            name = !login ? "RepositoryFolder" : "BookMC"


            if (login) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = !login ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}
