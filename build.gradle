plugins {
    id "java"
    id "maven-publish"
    id "me.dreamhopping.pml.gradle" version "3.1.6-local"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

ext {
    def env = System.getenv()

    devLocal = false // Change this to true if you want local
    local = env.GITHUB_ACTIONS ? false : devLocal
}

group "org.bookmc"
version "0.3.0${local ? "-local" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmc.org/"
    }
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }

    if (local) {
        mavenLocal()
    }
}

minecraft {
    separateVersionJars()

    clientRunClass = "org.bookmc.loader.impl.launch.QuiltClient"
    serverRunClass = "org.bookmc.loader.impl.launch.QuiltServer"


    target("1.17.1") {
        accessTransformer "./src/mc1.17.1/resources/accessor.json"
        yarn()
    }

    target "1.16.5", "1.15.2", "1.14.4", "1.13.2", "1.12.2", "1.11.2", "1.10.2", "1.9.4"
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-api:2.14.1"
    implementation "org.apache.logging.log4j:log4j-core:2.14.1"

    implementation "org.bookmc:book-loader:0.8.22+${local ? "local" : "build.1"}"

    include("org.bookmc:srg-processor:2.0.1") {
        exclude module: "log4j-api"
        exclude module: "log4j-core"
    }
}


jar {
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class"
}

publishing {
    repositories {
        maven {
            name "BookMC"

            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                url = "https://github.com/BookMC/${project.name}"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}